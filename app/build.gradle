apply plugin: 'com.android.application'

android {
    compileSdkVersion 34
    buildToolsVersion '34.0.0'

    def keyProps = new Properties()
    def keyPropsFile = rootProject.file('keystore/credentials')

    if (keyPropsFile.exists()) {
        keyProps.load(new FileInputStream(keyPropsFile))
    }


    signingConfigs {
        dev {
            v2SigningEnabled true
        }
        release {
            keyAlias keyProps['keyAlias']
            keyPassword keyProps['keyPassword']
            storeFile keyProps['storeFile'] ? file(keyProps['storeFile']) : null
            storePassword keyProps['storePassword']
        }
    }

    defaultConfig {
        applicationId "com.nass.ek.w3kiosk"
        minSdkVersion 19
        targetSdkVersion 33
        multiDexEnabled true
        versionCode rootProject.ext.verCode
        versionName rootProject.ext.verName

        setProperty("archivesBaseName", "w3kiosk-$versionName")

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            buildConfigField "String", "BUILD_TIME", "\"" + System.currentTimeMillis().toString() + "\""
            buildConfigField 'String', "API_NAME", API_NAME
            buildConfigField 'String', "API_KEY", API_KEY
            buildConfigField 'String', "API_URL", API_URL
            buildConfigField 'String', "UPDATE_URL", UPDATE_URL
        }
        release {
            buildConfigField "String", "BUILD_TIME", "\"" + System.currentTimeMillis().toString() + "\""
            buildConfigField 'String', "API_NAME", API_NAME
            buildConfigField 'String', "API_KEY", API_KEY
            buildConfigField 'String', "API_URL", API_URL
            buildConfigField 'String', "UPDATE_URL", UPDATE_URL

            minifyEnabled false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    namespace 'com.nass.ek.w3kiosk'
    lint {
        abortOnError false
        warningsAsErrors false
    }
    buildFeatures {
        buildConfig true
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.gridlayout:gridlayout:1.0.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.preference:preference:1.2.1'
    implementation 'com.google.android.material:material:1.11.0'
    implementation 'com.github.QuadFlask:colorpicker:0.0.13'
    implementation 'com.google.android.gms:play-services-location:21.1.0'
    implementation 'androidx.browser:browser:1.3.0'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation project(':appupdate')
}
